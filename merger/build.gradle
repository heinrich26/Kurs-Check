import org.apache.tools.ant.taskdefs.condition.Os
import org.beryx.runtime.JPackageTask

plugins {
    id 'myproject.java-conventions'
    id 'org.beryx.runtime' version '1.12.7'
}

application {
    mainClass = "${group}.gui.GuiMain"
}

dependencies {
    implementation project(':lib')
    implementation project(':app')

    implementation 'org.jetbrains.kotlinx:kotlinx-cli:0.3.6'
    implementation 'org.apache.commons:commons-csv:1.9.0'
}

copyInstallerResources.dependsOn(downloadSchoolsData)
jpackageImage.dependsOn copyInstallerResources

def buildDir = layout.buildDirectory
def (osPrefix, iconExt, packageType) =
    Os.isFamily(Os.FAMILY_WINDOWS) ? ['win', 'ico', 'msi'] :
            Os.isFamily(Os.FAMILY_MAC) ? ['mac', 'icns', 'pkg'] :
                    ['linux', 'png', 'deb']
List<String> javaModules = ['java.base', 'java.datatransfer', 'java.desktop', 'java.logging',
                            'java.management', 'java.net.http', 'java.prefs', 'java.sql',
                            'java.transaction.xa', 'java.xml', 'jdk.crypto.cryptoki', 'jdk.crypto.ec']

runtime {
    addOptions '--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages'

    additive = true
    modules.set(javaModules)

    jpackage {
        imageName = rootProject.name
        imageOptions = ['--add-launcher', "wahl-merger=res/$osPrefix-merger.properties",
                        '--icon', "$rootDir/res/icons/app_icon.$iconExt"]

        installerName = rootProject.name + '-full'
        installerOutputDir = file("$rootDir/out/executable/full")
        installerType = packageType
        installerOptions = ['--file-associations', buildDir.file("$resDirName/kurswahl-${osPrefix}.properties").get().toString(),
                            '--vendor', '"Willi-Graf-Gymnasium"',
                            '--description', "\"$description\"",
                            '--copyright', "\"Copyright Hendrik Horstmann ${Calendar.instance.get(Calendar.YEAR)}, Alle Rechte vorbehalten\"",
                            '--about-url', 'https://github.com/heinrich26/kurswahlApp',
                            '--license-file', "$rootDir/COPYRIGHT",
                            "@$rootDir/app/res/$osPrefix-opts.txt"]
        resourceDir = buildDir.dir(resDirName).get().asFile
    }
}

tasks.register('buildRPM', JPackageTask) {
    runtime {
        addOptions '--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages'

        additive = true
        modules.set(javaModules)

        jpackage {
            imageName = rootProject.name
            imageOptions = ['--add-launcher', 'wahl-merger=res/linux-merger.properties',
                            '--icon', "$rootDir/res/icons/app_icon.png"]

            installerName = rootProject.name + '-full'
            installerOutputDir = file("$rootDir/out/executable/full")
            installerType = 'rpm'
            installerOptions = ['--file-associations', buildDir.file("$resDirName/kurswahl-linux.properties").get().toString(),
                                '--vendor', '"Willi-Graf-Gymnasium"',
                                '--description', "\"$description\"",
                                '--copyright', "\"Copyright Hendrik Horstmann ${Calendar.instance.get(Calendar.YEAR)}, Alle Rechte vorbehalten\"",
                                '--about-url', 'https://github.com/heinrich26/kurswahlApp',
                                '--license-file', "$rootDir/COPYRIGHT",
                                "@$rootDir/app/res/linux-opts.txt"]
            resourceDir = buildDir.dir(resDirName).get().asFile
        }
    }
}
