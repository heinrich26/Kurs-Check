import org.apache.tools.ant.taskdefs.condition.Os

plugins {
    id 'myproject.java-conventions'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'edu.sc.seis.launch4j' version '2.5.3'
    id 'org.beryx.runtime' version '1.12.7'
}

apply plugin: 'com.github.johnrengelman.shadow'

application {
    mainClass = 'gui.GuiMain'
}
def artifactName = 'wahl-merger'

dependencies {
    implementation project(':lib')
    implementation project(':app')

    implementation 'org.jetbrains.kotlinx:kotlinx-cli:0.3.4'
    implementation 'org.apache.commons:commons-csv:1.9.0'
}

launch4j { // Baut die GUI exe
    icon = "${rootProject.projectDir}/icons/app_icon.ico"
    copyConfigurable = []
    jarTask = project.tasks.shadowJar
    headerType = "console"
    outfile = "${artifactName}.exe"
    fileDescription = "Kommandozeilen-Anwendung zum Zusammenf√ºhren der .kurswahl Dateien! Siehe: ${outfile} --help"
}

tasks.register('prepInstallerRes', Delete) {
    delete layout.buildDirectory.dir(resDirName), layout.buildDirectory.dir('temp')
}
copyInstallerResources.dependsOn prepInstallerRes
tasks.getByName('jpackageImage').dependsOn copyInstallerResources

runtime {
    jpackage {
        installerOutputDir = file("$rootDir/out/executable/full")
        imageName = rootProject.name
        installerName = rootProject.name + '-full'
        imageOptions = ['--add-launcher', "wahl-merger=res/${(Os.isFamily(Os.FAMILY_WINDOWS)) ? 'win' : 'mac'}-merger.properties"]

        installerType = (Os.isFamily(Os.FAMILY_WINDOWS)) ? 'msi' : 'pkg'

        installerOptions = ['--file-associations', "${rootProject.projectDir}/app/res/kurswahl.properties",
                            '--vendor', '"Willi-Graf-Gymnasium"', '--description', "\"$description\"",
                            '--copyright', '"Copyright 2022, All rights reserved"',
                            '--about-url', 'https://github.com/heinrich26/kurswahlApp',
                            (Os.isFamily(Os.FAMILY_WINDOWS)) ? '@res/win-opts.txt' : '@res/mac-opts.txt',
                            '--temp', "$buildDir/temp"]
        resourceDir = file("$buildDir/$resDirName")
    }

    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
}
