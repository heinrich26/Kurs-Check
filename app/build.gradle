import org.apache.tools.ant.taskdefs.condition.Os
import org.beryx.runtime.JPackageTask

plugins {
    id 'myproject.java-conventions'
    id 'org.beryx.runtime' version '1.12.7'
    id "org.jetbrains.gradle.plugin.idea-ext" version "1.1.7"
}


dependencies {
    implementation project(':lib')

    implementation 'com.fasterxml.jackson.module:jackson-module-kotlin:2.13.3'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.13.3'
    implementation 'org.jetbrains.kotlinx:kotlinx-cli:0.3.4'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.4'
    implementation 'com.github.lgooddatepicker:LGoodDatePicker:11.2.1'
}

sourceSets {
    main.kotlin.srcDirs += "build/generated/sources/vectors/main"
}
idea.module.settings.packagePrefix["src/main/kotlin"] = group

kotlin {
    jvmToolchain(18)
}

application {
    mainClass = "${group}.gui.GuiMain"
}

compileKotlin.dependsOn(':resource-generator:processVectors')
jpackageImage.dependsOn(copyInstallerResources, downloadSchoolsData)


String osPrefix = (Os.isFamily(Os.FAMILY_WINDOWS)) ? 'win' : ((Os.isFamily(Os.FAMILY_MAC)) ? 'mac' : 'linux')
String iconExt = (Os.isFamily(Os.FAMILY_WINDOWS)) ? 'ico' : ((Os.isFamily(Os.FAMILY_MAC)) ? 'icns' : 'png')
runtime {
    addOptions '--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages'

    additive = true
    modules.set(['jdk.crypto.cryptoki'])

    jpackage {
        imageName = rootProject.name
        imageOptions = ['--icon', "$rootDir/res/icons/app_icon.$iconExt"]

        installerName = rootProject.name
        installerOutputDir = file("$rootDir/out/executable/basic")
        installerType = (Os.isFamily(Os.FAMILY_WINDOWS)) ? 'msi' : ((Os.isFamily(Os.FAMILY_MAC)) ? 'pkg' : 'deb')
        installerOptions = ['--file-associations', "$buildDir/$resDirName/kurswahl-${osPrefix}.properties",
                            '--vendor', '"Willi-Graf-Gymnasium"',
                            '--description', "\"$description\"",
                            '--copyright', '"Copyright Hendrik Horstmann 2023, All rights reserved"',
                            '--about-url', 'https://github.com/heinrich26/kurswahlApp',
                            '--license-file', "$rootDir/COPYRIGHT",
                            '--verbose',
                            "@res/$osPrefix-opts.txt"]

        resourceDir = file("$buildDir/$resDirName")
    }
}

tasks.register('buildRPM', JPackageTask) {
    runtime {
        addOptions '--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages'

        additive = true
        modules.set(['jdk.crypto.cryptoki'])

        jpackage {
            imageName = rootProject.name
            imageOptions = ['--icon', "$rootDir/res/icons/app_icon.png"]

            installerName = rootProject.name
            installerOutputDir = file("$rootDir/out/executable/basic")
            installerType = 'rpm'
            installerOptions = ['--file-associations', "$buildDir/$resDirName/kurswahl-linux.properties",
                                '--vendor', '"Willi-Graf-Gymnasium"',
                                '--description', "\"$description\"",
                                '--copyright', '"Copyright Hendrik Horstmann 2023, All rights reserved"',
                                '--about-url', 'https://github.com/heinrich26/kurswahlApp',
                                '--license-file', "$rootDir/COPYRIGHT",
                                '--verbose',
                                "@res/linux-opts.txt"]

            resourceDir = file("$buildDir/$resDirName")
        }
    }
}
